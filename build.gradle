plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

allprojects {
    group = 'com.tcoded'
    version = '2.7.4'

    repositories {
        mavenCentral()

        maven {
            name = "codemc-releases"
            url = "https://repo.codemc.io/repository/maven-releases/"
        }

        maven {
            name = "codemc-snapshots"
            url = "https://repo.codemc.io/repository/maven-snapshots/"
        }

        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }

        maven { url 'https://jitpack.io' }

        maven { url 'https://repo.papermc.io/repository/maven-public' }

        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }

        maven {
            name = "problems"
            url = "https://repo.viaversion.com"
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'

    group = rootProject.group + '.nochatreports'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    jar {
        manifest {}
    }

//    shadowJar {
//        minimize() {
//            logger.log(LogLevel.ERROR, "Minimizing ${project.name}")
//            exclude project(':nms')
//            exclude "META-INF", "META-INF/**"
//        }
//    }

    test {
        useJUnitPlatform()
    }

}

tasks.assemble {
    dependsOn shadowJar
}

dependencies {
    implementation project(":plugin")
    implementation project(":nms:common")
//    implementation project(":nms:v1_19_4")
    implementation project(path: ":nms:v1_19_4", configuration: "reobf")
    implementation project(path: ":nms:v1_20", configuration: "reobf")
    implementation project(path: ":nms:v1_20_2", configuration: "reobf")
    implementation project(path: ":nms:v1_20_3", configuration: "reobf")
    implementation project(path: ":nms:v1_20_4", configuration: "reobf")
    implementation project(path: ":nms:v1_20_6", configuration: "reobf")
    implementation project(path: ":nms:v1_21", configuration: "reobf")
    implementation project(path: ":nms:v1_21_1", configuration: "reobf")
    implementation project(path: ":nms:v1_21_3", configuration: "reobf")
    implementation project(path: ":nms:v1_21_4", configuration: "reobf")
    implementation project(path: ":nms:v1_21_5", configuration: "reobf")
    implementation project(path: ":nms:v1_21_6", configuration: "reobf")
    implementation project(path: ":nms:v1_21_7", configuration: "reobf")
    implementation project(path: ":nms:v1_21_8", configuration: "reobf")
    implementation project(path: ":nms:v1_21_9", configuration: "reobf")

//    implementation "com.github.retrooper:packetevents-spigot:2.7.0-SNAPSHOT"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    archiveClassifier.set("raw")
}

shadowJar {
    archiveClassifier.set("")

    // PacketEvents
    relocate 'com.github.retrooper.packetevents', "${project.group}.nochatreports.lib.packetevents.api"
    relocate 'io.github.retrooper.packetevents', "${project.group}.nochatreports.lib.packetevents.impl"
    relocate 'net.kyori', "${project.group}.nochatreports.lib.packetevents.kyori"

    // SpigotUpdateChecker
    relocate 'com.tcoded.lightlibs.updatechecker', "${project.group}.nochatreports.lib.spigotupdatechecker"

    // FoliaLib
    relocate "com.tcoded.folialib", "com.tcoded.nochatreports.plugin.lib.folialib"
}